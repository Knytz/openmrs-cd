apply plugin: 'distribution'

distZip.enabled = true
distTar.enabled = false

allprojects {
  group = 'net.mekomsolutions'
  version = '3.0.0-SNAPSHOT'
}

// subprojects are supposed to copy their output files under /openmrs-cd inside the main build dir
distributions {
  main {
    baseName = 'openmrs-cd'
    contents {
      from { "$buildDir/openmrs-cd" }
    }
  }
}

apply plugin: 'maven-publish'

// this is to work both locally (via gradle.properties) and on Travis CI (via envvars)
def repoUrl = hasProperty('mksRepoUrl') ? mksRepoUrl : System.getenv('mksRepoUrl')
def repoUsername = hasProperty('mksRepoUsername') ? mksRepoUsername : System.getenv('mksRepoUsername')
def repoPassword = hasProperty('mksRepoPassword') ? mksRepoPassword : System.getenv('mksRepoPassword')

publishing {
  publications {
    myDistribution(MavenPublication) {
      artifact distZip
    }
  }
  repositories {
    maven {
      name 'mks-repo'
      url repoUrl
      credentials {
        username = repoUsername
        password = repoPassword
      }
    }
  }
}

task deploy() {
  dependsOn assemble, publish
}